
#Область ПрограммныйИнтерфейс

// Выполняет шифрование заданного текста с использованием указанного открытого ключа.
//
// Параметры:
//   Текст - Строка - исходные данные для шифрования
//   ОткрытыйКлюч - Строка - ключ, используемый для шифрования данных
//
// Возвращаемое значение:
//   Строка - зашифрованные данные в формате Base64
Функция Зашифровать(Текст, ОткрытыйКлюч) Экспорт

	Если Не ЗначениеЗаполнено(Текст) Или Не ЗначениеЗаполнено(ОткрытыйКлюч) Тогда
		Возврат "";
	КонецЕсли;
	
	ПутьФайла = ПолучитьИмяВременногоФайла();
	
	ЗаписьТекста = Новый ЗаписьТекста(ПутьФайла);
	ЗаписьТекста.Записать(Текст);
	ЗаписьТекста.Закрыть();
	
	Архив = Новый ЗаписьФайлаАрхива();
	Архив.Добавить(ПутьФайла);
	Данные = Архив.ПолучитьДвоичныеДанные(ОткрытыйКлюч, ТипФайлаАрхива.ZIP, "", 
		МетодСжатияФайлаАрхива.Сжатие, УровеньСжатияФайлаАрхива.Оптимальный, МетодШифрованияФайлаАрхива.AES256);
	УдалитьФайлы(ПутьФайла);
	
	Возврат Base64Строка(Данные);
	
КонецФункции

// Расшифровывает данные, используя указанный открытый ключ.
//
// Параметры:
//   Данные - Строка - Зашифрованные данные в формате Base64
//   ОткрытыйКлюч - Строка - Открытый ключ для расшифровки
//
// Возвращаемое значение:
//   Строка - Расшифрованные данные
Функция Расшифровать(Данные, ОткрытыйКлюч) Экспорт
	
	Если Не ЗначениеЗаполнено(Данные) Или Не ЗначениеЗаполнено(ОткрытыйКлюч) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Поток = Новый ПотокВПамяти();
	ДвоичныеДанные = Base64Значение(Данные);
	ДвоичныеДанные.Записать(Поток);
	
	Каталог = КаталогВременныхФайлов();
	Архив = Новый ЧтениеФайлаАрхива(Поток, ОткрытыйКлюч, ТипФайлаАрхива.ZIP);
	Если Архив.Элементы.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	Попытка
		Архив.Извлечь(Архив.Элементы[0], Каталог, РежимВосстановленияПутейФайлаАрхива.НеВосстанавливать);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	ПолноеИмяФайла = Каталог + ПолучитьРазделительПутиСервера() + Архив.Элементы[0].Имя;
	Архив.Закрыть();
	
	ЧтениеТекста = Новый ЧтениеТекста(ПолноеИмяФайла);
	Результат = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	УдалитьФайлы(ПолноеИмяФайла);
	
	Возврат Результат;
	
КонецФункции

Функция ОбновитьЛокальноеХранилищеСекретов(ОткрытыйКлюч, Секрет = Неопределено) Экспорт
	НаборЗаписей = РегистрыСведений.ЛокальноеХранилище.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(ПараметрыСеанса.ТекущийПользователь);
	Если ЗначениеЗаполнено(Секрет) Тогда
		НаборЗаписей.Отбор.Секрет.Установить(Секрет);
	Иначе
		ДобавитьЗаписьЛокальногоХранилища(НаборЗаписей, Неопределено, БазовоеСообщениеСекрета(), ОткрытыйКлюч);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Секреты.Ссылка КАК Секрет
		|ИЗ
		|	Справочник.Секреты КАК Секреты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПраваДоступа КАК ПраваДоступа
		|		ПО (ПраваДоступа.СекретСейф В (Секреты.Ссылка, Секреты.Сейф))
		|ГДЕ
		|	Секреты.ТипСекрета <> ЗНАЧЕНИЕ(Перечисление.ТипыСекретов.Локальный)
		|	И &Секрет В (НЕОПРЕДЕЛЕНО, Секреты.Ссылка)
		|	И Секреты.Ссылка <> ЗНАЧЕНИЕ(Справочник.Секреты.ПустаяСсылка)";
	Запрос.УстановитьПараметр("ТекущийПользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Секрет", Секрет);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеСекрета = ИнтеграцияOpenBao.ПолучитьСекрет(Выборка.Секрет);
		ТекстСекрета = КоннекторHTTP.ОбъектВJson(ДанныеСекрета);
		ДобавитьЗаписьЛокальногоХранилища(НаборЗаписей, Выборка.Секрет, ТекстСекрета, ОткрытыйКлюч);
	КонецЦикла;
	НаборЗаписей.Записать();
КонецФункции

Функция МастерПарольУжеУстановлен() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЛокальноеХранилище.Пользователь КАК Пользователь,
		|	ЛокальноеХранилище.Секрет КАК Секрет
		|ИЗ
		|	РегистрСведений.ЛокальноеХранилище КАК ЛокальноеХранилище
		|ГДЕ
		|	ЛокальноеХранилище.Пользователь = &Пользователь
		|	И ЛокальноеХранилище.Секрет = ЗНАЧЕНИЕ(Справочник.Секреты.ПустаяСсылка)";
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Возврат Не Запрос.Выполнить().Пустой();
КонецФункции

Функция ПроверитьМастерПароль(ОткрытыйКлюч) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЛокальноеХранилище.ЗашифрованноеЗначение КАК ЗашифрованноеЗначение
		|ИЗ
		|	РегистрСведений.ЛокальноеХранилище КАК ЛокальноеХранилище
		|ГДЕ
		|	ЛокальноеХранилище.Пользователь = &Пользователь
		|	И ЛокальноеХранилище.Секрет = ЗНАЧЕНИЕ(Справочник.Секреты.ПустаяСсылка)";
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		ЗаписатьБазовоеСообщениеСекрета(ОткрытыйКлюч);
		Возврат Истина;
	КонецЕсли;
	Расшифровка = Расшифровать(Выборка.ЗашифрованноеЗначение.Получить(), ОткрытыйКлюч);
	Возврат Расшифровка = БазовоеСообщениеСекрета();
КонецФункции

Функция ПолучитьСекрет(Секрет, ОткрытыйКлюч) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЛокальноеХранилище.ЗашифрованноеЗначение КАК ЗашифрованноеЗначение
		|ИЗ
		|	РегистрСведений.ЛокальноеХранилище КАК ЛокальноеХранилище
		|ГДЕ
		|	ЛокальноеХранилище.Пользователь = &Пользователь
		|	И ЛокальноеХранилище.Секрет = &Секрет";
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	Запрос.УстановитьПараметр("Секрет", Секрет);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат НастройкиКонфигурацииПовтИсп.ДанныеНовогоСекретапоУмолчанию();
	КонецЕсли;
	Расшифровка = Расшифровать(Выборка.ЗашифрованноеЗначение.Получить(), ОткрытыйКлюч);
	Возврат КоннекторHTTP.JsonВОбъект(Расшифровка);
КонецФункции

Функция ЗаписатьСекрет(Секрет, ДанныеСекрета, ОткрытыйКлюч) Экспорт
	ТекстСекрета = КоннекторHTTP.ОбъектВJson(ДанныеСекрета);
	НаборЗаписей = РегистрыСведений.ЛокальноеХранилище.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(ПараметрыСеанса.ТекущийПользователь);
	НаборЗаписей.Отбор.Секрет.Установить(Секрет);
	ДобавитьЗаписьЛокальногоХранилища(НаборЗаписей, Секрет, ТекстСекрета, ОткрытыйКлюч);
	НаборЗаписей.Записать();
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция БазовоеСообщениеСекрета()
	Возврат "СекретноеМесто2025";
КонецФункции

Процедура ДобавитьЗаписьЛокальногоХранилища(НаборЗаписей, Секрет, ТекстСекрета, ОткрытыйКлюч)
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	НоваяЗапись.Секрет = Секрет;
	НоваяЗапись.ЗашифрованноеЗначение = Новый ХранилищеЗначения(Зашифровать(ТекстСекрета, ОткрытыйКлюч));
КонецПроцедуры

Процедура ЗаписатьБазовоеСообщениеСекрета(ОткрытыйКлюч)
	НаборЗаписей = РегистрыСведений.ЛокальноеХранилище.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(ПараметрыСеанса.ТекущийПользователь);
	ДобавитьЗаписьЛокальногоХранилища(НаборЗаписей, Неопределено, БазовоеСообщениеСекрета(), ОткрытыйКлюч);
	НаборЗаписей.Записать();
КонецПроцедуры

#КонецОбласти
