
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаявкаНаПубликацию.Ссылка КАК Ссылка
		|ИЗ
		|	БизнесПроцесс.ЗаявкаНаПубликацию КАК ЗаявкаНаПубликацию
		|ГДЕ
		|	Не ЗаявкаНаПубликацию.ПометкаУдаления
		|	И ЗаявкаНаПубликацию.Секрет = &Секрет
		|	И ЗаявкаНаПубликацию.Стартован
		|	И Не ЗаявкаНаПубликацию.Завершен";
	Запрос.УстановитьПараметр("Секрет", Объект.Ссылка);
	
	ЗапросЛокальноеХранилище = Новый Запрос;
	ЗапросЛокальноеХранилище.Текст =
		"ВЫБРАТЬ
		|	ЛокальноеХранилище.Модифицированность КАК Модифицированность
		|ИЗ
		|	РегистрСведений.ЛокальноеХранилище КАК ЛокальноеХранилище
		|ГДЕ
		|	ЛокальноеХранилище.Пользователь = &Пользователь
		|	И ЛокальноеХранилище.Секрет = &Секрет";
	ЗапросЛокальноеХранилище.УстановитьПараметр("Секрет", Объект.Ссылка);
	ЗапросЛокальноеХранилище.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	ВыборкаЛокальноеХранилище = ЗапросЛокальноеХранилище.Выполнить().Выбрать();
	ВыборкаЛокальноеХранилище.Следующий();
	
	Элементы.ФормаСоздатьЗаявкуНаПубликацию.Видимость = (Объект.ТипСекрета = Перечисления.ТипыСекретов.Локальный) 
		Или ВыборкаЛокальноеХранилище.Модифицированность = Истина;
	Элементы.ФормаСоздатьЗаявкуНаПубликацию.Доступность = Запрос.Выполнить().Пустой();
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ТипСекрета = Перечисления.ТипыСекретов.Локальный;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСекрет(Команда)
	Если Модифицированность Тогда
		Сообщить("Элемент необходимо записать");
		Возврат;
	КонецЕсли;
	Если ЗначениеЗаполнено(МастерПарольКлиент.Получить()) Тогда
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Секрет", Объект.Ссылка);
		ПараметрыОткрытия.Вставить("ОткрытыйКлюч", МастерПарольКлиент.Получить());
		ПараметрыОткрытия.Вставить("ДоступноИзменение", ДоступноИзменение());
		ОткрытьФорму("Справочник.Секреты.Форма.ФормаДанныхСекрета", ПараметрыОткрытия);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаявкуНаПубликацию(Команда)
	Если Модифицированность Тогда
		Сообщить("Элемент необходимо записать");
		Возврат;
	КонецЕсли;
	Если Объект.Сейф = ПредопределенноеЗначение("Справочник.Сейфы.Личный") Тогда
		Сообщить("Нельзя публиковать секреты из личной папки");
		Возврат;
	КонецЕсли;
	СоздатьЗаявкуНаПубликациюНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьЗаявкуНаПубликациюНаСервере()
	Элементы.ФормаСоздатьЗаявкуНаПубликацию.Доступность = Ложь;
	БП = БизнесПроцессы.ЗаявкаНаПубликацию.СоздатьБизнесПроцесс();
	БП.Дата = ТекущаяДатаСеанса();
	БП.Пользователь = ПараметрыСеанса.ТекущийПользователь;
	БП.Секрет = Объект.Ссылка;
	БП.Записать();
	БП.Старт();
	Сообщить("Стартован БП " + БП);
КонецПроцедуры

Функция ДоступноИзменение()
	Возврат Объект.ТипСекрета = ПредопределенноеЗначение("Перечисление.ТипыСекретов.Локальный") 
		Или РольДоступна(Метаданные.Роли.ИзменениеСекретов) 
		Или РольДоступна(Метаданные.Роли.ПолныеПрава);
КонецФункции