&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Пользователь = Объект.БизнесПроцесс.Пользователь;
	Секрет = Объект.БизнесПроцесс.Секрет;
	Если Пользователь <> ПараметрыСеанса.ТекущийПользователь Тогда
		Сообщить("Задачу может выполнять только " + Пользователь);
		ТолькоПросмотр = Истина;
		Элементы.Опубликовать.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОпубликоватьНаСервере(ОткрытыйКлюч)
	ЗначениеСектера = ЛокальноеХранилище.ПолучитьСекрет(Объект.БизнесПроцесс.Секрет, ОткрытыйКлюч);
	ИнтеграцияOpenBao.ЗаписатьСекрет(Объект.БизнесПроцесс.Секрет, ЗначениеСектера);
	ЛокальноеХранилище.ЗаписатьСекрет(Объект.БизнесПроцесс.Секрет, ЗначениеСектера, ОткрытыйКлюч, Ложь);
	ЗадачаОбъект = РеквизитФормыВЗначение("Объект");
	ЗадачаОбъект.ВыполнитьЗадачу();
	СекретОбъект = Объект.БизнесПроцесс.Секрет.ПолучитьОбъект();
	СекретОбъект.ТипСекрета = Перечисления.ТипыСекретов.Общий;
	СекретОбъект.Записать();
КонецПроцедуры

&НаКлиенте
Процедура Опубликовать(Команда)
	Если ЗначениеЗаполнено(МастерПарольКлиент.Получить()) Тогда
		ОпубликоватьНаСервере(МастерПарольКлиент.Получить());
		Оповестить("ОбновитьСписокЗадач");
		Закрыть();
	КонецЕсли;
КонецПроцедуры
